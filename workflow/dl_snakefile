rule get_hm3_snplist:
    output:
        temp(config_d['DL'] +"hapmap3_snps.tgz")
    shell:
        "wget https://data.broadinstitute.org/alkesgroup/LDSCORE/hapmap3_snps.tgz -O {output}"

rule gunzip_hm3:
    input:
        rules.get_hm3_snplist.output
    params:
        dld=config_d['1KG']
    output:
        expand(config_d['1KG']+"hapmap3_snps/"+"hm.{chrom}.snp",chrom=range(1,23))
    shell:
        "tar -C {params.dld} -xvzf {input}"

# rule snp2coord:
#     input:
#         inputf=config_d['1KG']+"hapmap3_snps/"+"hm.{chrom}.snp"
#     output:
#         outputf=config_d['1KG']+"hapmap3_snps/"+"hm.{chrom}.tsv.gz"
#     script:
#         "../scripts/rsid2loc.R"

rule get_baseline_model:
    output:
        temp(config_d['DL']+"1000G_Phase3_baselineLD_v2.2_ldscores.tgz")
    shell:
        "wget https://data.broadinstitute.org/alkesgroup/LDSCORE/1000G_Phase3_baselineLD_v2.2_ldscores.tgz -O {output}"

rule get_weights:
    output:
        temp(config_d["DL"]+"1000G_Phase3_weights_hm3_no_MHC.tgz")
    shell:
        "wget https://data.broadinstitute.org/alkesgroup/LDSCORE/1000G_Phase3_weights_hm3_no_MHC.tgz -O {output}"

rule gunzip_weights:
    input:
        config_d["DL"]+"1000G_Phase3_weights_hm3_no_MHC.tgz"
    output:
        ldfiles = expand(config_d['WEIGHTS'] +"weights.hm3_noMHC.{chrom}.l2.ldscore.gz",chrom=range(1,23))
    params:
        W=config_d['WEIGHTS']
    shell:
        "tar -xvzf {input} -C {params.W}"        

rule get_frq:
    output:
        temp(config_d['DL']+"1000G_Phase3_frq.tgz")
    shell:
        "wget https://data.broadinstitute.org/alkesgroup/LDSCORE/1000G_Phase3_frq.tgz -O {output}"


rule get_plinkfiles:
    output:
        temp(config_d['DL'] +"1000G_Phase3_plinkfiles.tgz")
    shell:
        "wget https://data.broadinstitute.org/alkesgroup/LDSCORE/1000G_Phase3_plinkfiles.tgz -O {output}"



rule gunzip_plinkfiles:
    input:
        config_d['DL'] +"1000G_Phase3_plinkfiles.tgz"
    output:
        fam_files = expand(config_d['1KG'] +"1000G_EUR_Phase3_plink/1000G.EUR.QC.{chrom}.fam",chrom=range(1,23)),
        bim_files = expand(config_d['1KG'] +"1000G_EUR_Phase3_plink/1000G.EUR.QC.{chrom}.bim",chrom=range(1,23)),
        bed_files = expand(config_d['1KG'] +"1000G_EUR_Phase3_plink/1000G.EUR.QC.{chrom}.bed",chrom=range(1,23))
    params:
        KG=config_d['1KG']
    shell:
        "tar -xvzf {input} -C {params.KG}"

rule gunzip_frqf:
    input:
        config_d['DL'] +"1000G_Phase3_frq.tgz"
    output:
        fam_files = expand(config_d['FRQF'] +"1000G.EUR.QC.{chrom}.frq",chrom=range(1,23)),
    params:
        KG=config_d['FRQF']
    shell:
        "tar -xvzf {input} -C {params.KG}"


rule gunzip_baseline:
    input:
        config_d['DL'] +"1000G_Phase3_baseline_ldscores.tgz"
    output:
        ldfiles = expand(config_d['L2'] +"baseline/baselineLD.{chrom}.l2.ldscore.gz",chrom=range(1,23)),
        annotf = expand(config_d['L2'] +"baseline/baselineLD.{chrom}.annot.gz",chrom=range(1,23)),
        m50 = expand(config_d['L2'] +"baseline/baselineLD.{chrom}.l2.M_5_50",chrom=range(1,23)),       
    params:
        L2=config_d['L2']
    shell:
        "tar -xvzf {input} -C {params.L2}"



rule unzip_annot:
    input:
        config_d['BED'] + "{annot}.bed.bz2"
    output:
        temp(config_d['BED'] + "{annot}.bed")
    shell:
        "bzip2 -cd {input} > {output}"


rule make_annot:
    input:
        anno_bed=config_d['BED'] +"{annot}.bed",
        bim=config_d['1KG'] + "1000G_EUR_Phase3_plink/1000G.EUR.QC.{chrom}.bim"
    output:
        annot = config_d['ANNO'] +"{annot}/{annot}.{chrom}.annot.gz"
    params:
        anno_name='{annot}'
    conda:
        "../envs/ldsc.yml"
    shell:
        "python2 ../make_annot.py --bed-file {input.anno_bed} --bimfile {input.bim} --annot-file {output.annot}"



rule cmp_ldscores:
    input:
        anno_bed=config_d['ANNO'] +"{annot}/{annot}.{chrom}.annot.gz",
        bim=config_d['1KG'] + "1000G_EUR_Phase3_plink/1000G.EUR.QC.{chrom}.bim",
        bed=config_d['1KG'] + "1000G_EUR_Phase3_plink/1000G.EUR.QC.{chrom}.bed",
        fam=config_d['1KG'] + "1000G_EUR_Phase3_plink/1000G.EUR.QC.{chrom}.fam"
    output:
        tempf=temp(config_d['L2']+"{annot}.{chrom}.log"),
        l2=config_d['L2']+"{annot}.{chrom}.l2.M",
        l2M_50=config_d['L2']+"{annot}.{chrom}.l2.M_5_50",
        l2gz=config_d['L2']+"{annot}.{chrom}.l2.ldscore.gz"
    params:
        plink=config_d['1KG'] + "1000G_EUR_Phase3_plink/1000G.EUR.QC.{chrom}",
        odir=config_d['L2']+"{annot}.{chrom}"
    conda:
        "../envs/ldsc.yml"
    shell:
        "python2 ../ldsc.py --l2 --bfile {params.plink} --ld-wind-cm 1 --annot {input.anno_bed} --thin-annot --out {params.odir} "

# def ldsc_fun(wildcards):
#     {tchrom: expand(config_d['L2'] +"{anno_name}.{chrom}.l2.ldscore.gz",chrom=[tchrom],anno_name=['baseline' *all_annot[wildcards.anno_name]]) for tchrom in range(1,23)}

# rule check_ldsc:
#     input:
#         unpack(ldsc_fun)
#     output:
#         temp("{anno_name}.check")
#     script:
#         "../scripts/check_ldscfiles.R"

rule run_ldsc:
    input:
        unpack(get_annot_files)
    output:
        dataf="{anno_name}.results"
    log:
        tempf=temp("{anno_name}.log")
    params:
      annot=lambda wildcards: ','.join(expand(config_d['L2']+"{anno_name}.",anno_name=all_annot[wildcards.anno_name])),
      baseline=config_d['L2']+"baseline/baselineLD.",
      weights=config_d['WEIGHTS']+"weights.hm3_noMHC.",
      frq=config_d['FRQF'] +"1000G.EUR.QC.",
      odir="{anno_name}"
    conda:
        "../envs/ldsc.yml"
    shell:
        """python2 ../ldsc.py --h2 {input.gwasf} --ref-ld-chr {params.annot},{params.baseline} --w-ld-chr {params.weights} --thin-annot --overlap-annot --frqfile-chr {params.frq} --out {params.odir} """
